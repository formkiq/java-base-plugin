plugins {
  id 'java-gradle-plugin'
  id "com.gradle.plugin-publish" version "2.0.0"
  id 'com.diffplug.spotless' version '7.2.1'
}

group   'com.formkiq.gradle'
version '1.0.6'

allprojects {
  apply plugin: 'com.diffplug.spotless'
}

repositories {
  gradlePluginPortal()
}

dependencies {
  implementation 'com.github.spotbugs:com.github.spotbugs.gradle.plugin:6.4.1'
  implementation 'com.diffplug.spotless:com.diffplug.spotless.gradle.plugin:7.2.1'
  implementation 'com.github.ben-manes.versions:com.github.ben-manes.versions.gradle.plugin:0.52.0'
  implementation 'com.formkiq.gradle.graalvm-native-plugin:com.formkiq.gradle.graalvm-native-plugin.gradle.plugin:1.7.4'
  testImplementation platform("org.spockframework:spock-bom:2.3-groovy-4.0")
  testImplementation 'org.spockframework:spock-core'
}

tasks.named('test', Test) {
  useJUnitPlatform()
}

spotless {
  spotless {
    java {
      eclipse().sortMembersEnabled(true).configFile rootProject.file("spotless.eclipseformat.xml")
    }
  }
  groovyGradle {
    target '**/*.gradle'
    greclipse()
    leadingTabsToSpaces(2)
    trimTrailingWhitespace()
    endWithNewline()
  }
}

spotlessCheck.dependsOn spotlessApply

gradlePlugin {
  website = 'https://github.com/formkiq/java-base-plugin'
  vcsUrl  = 'https://github.com/formkiq/java-base-plugin'
  plugins {
    javaBase {
      id = "com.formkiq.gradle.java-base"
      displayName = "FormKiQ Java Base Conventions"
      description = "Applies Java 17 toolchain, Spotless, SpotBugs, Checkstyle, Versions plugin, and standard repos."
      implementationClass = "com.formkiq.gradle.JavaBasePlugin"
      tags = [
        "java",
        "conventions",
        "spotless",
        "spotbugs",
        "checkstyle"
      ]
    }
  }
}

publishing {
  publications {
    pluginMaven(MavenPublication) {
    }
  }
}

spotlessJavaCheck.dependsOn 'spotlessJavaApply'
check.dependsOn(tasks.publishToMavenLocal)
